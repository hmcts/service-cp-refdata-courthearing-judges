plugins {
  id 'application'
  id 'java'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.3'
  id 'jacoco'
  id 'maven-publish'
  id "com.github.ben-manes.versions" version "0.52.0"
  id "org.cyclonedx.bom" version "2.3.1"
  id "au.com.dius.pact" version "4.6.17"
}

group = 'uk.gov.hmcts.cp'
version = System.getProperty('ARTEFACT_VERSION') ?: '0.0.999'

def githubActor = project.findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
def githubRepo = System.getenv("GITHUB_REPOSITORY")

def azureADOArtifactRepository = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
def azureADOArtifactActor = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
def azureADOArtifactToken = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")

//debugging
//    if (githubActor != null) {
//      println "üîê Configuring GitHub Packages publishing to: https://maven.pkg.github.com/$githubRepo"
//    }
//    println "GitHub Packages publishing required environment variables:"
//    println " - GITHUB_ACTOR=${githubActor != null ? ' ‚úî FOUND' : '‚ùå'}"
//    println " - GITHUB_TOKEN=${githubToken != null ? ' ‚úî FOUND' : '‚ùå'}"
//    println " - GITHUB_REPOSITORY=${githubRepo != null ? ' ‚úî FOUND' : '‚ùå'}"

//println "Azure ADO publishing required environment variables:"
//println " - AZURE_DEVOPS_ARTIFACT_USERNAME=${!azureADOArtifactActor || azureADOArtifactActor.isBlank() ? '‚ùåNULL or EMPTY' : ' ‚úî FOUND'}"
//println " - AZURE_DEVOPS_ARTIFACT_USERNAME=${!azureADOArtifactToken || azureADOArtifactToken.isBlank() ? '‚ùåNULL or EMPTY' : ' ‚úî FOUND'}"

java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly
}


tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.named('test') {
  useJUnitPlatform{
    excludeTags 'pact'
  }
  systemProperty 'API_SPEC_VERSION', project.version
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('pactVerificationTest', Test) {
  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = sourceSets.test.runtimeClasspath

  doFirst {
    println "üîç Pact Test - Checking JVM Properties:"
    println " - pact.broker.url = " + System.getProperty("pact.broker.url")
    println " - pact.broker.token = " + (System.getProperty("pact.broker.token") != null ? "‚úî set" : "‚ùå missing")
    println " - pact.provider.branch = " + System.getProperty("pact.provider.branch")
    println " - pact.verifier.publishResults = " + System.getProperty("pact.verifier.publishResults")
  }
  useJUnitPlatform {
    includeTags 'pact'
  }
  // this eases local development, but should not be used in CI
  if (!System.getProperty('pact.broker.url')) {
    def pact_broker_url = System.getenv('PACT_BROKER_URL')
    println "'pact.broker.url' not set, using environment variable PACT_BROKER_URL: " + pact_broker_url
    systemProperty 'pact.broker.url', pact_broker_url
  }
  if (!System.getProperty('pact.broker.host')) {
    def pact_broker_host = System.getenv('PACT_BROKER_HOST')
    println "'pact.broker.host' not set, using environment variable PACT_BROKER_HOST: " + pact_broker_host
    systemProperty 'pact.broker.host', pact_broker_host
  }
  if (!System.getProperty('pact.broker.token')) {
    def pact_broker_token = System.getenv('PACT_BROKER_TOKEN')
    println "'pact.broker.token' not set, using environment variable PACT_BROKER_TOKEN: " + ( pact_broker_token != null ? '******' : '‚ùå missing' )
    systemProperty 'pact.broker.token', pact_broker_token
  }
  if (!System.getProperty('pact.broker.version')) {
    def pact_broker_version = System.getenv('GIT_COMMIT') ?: System.getenv('ARTEFACT_VERSION')
    println "'pact.broker.version' not set, using environment variable GIT_COMMIT/ARTEFACT_VERSION: " + pact_broker_version
    systemProperty 'pact.provider.version', pact_broker_version
  }
  if (!System.getProperty('pact.provider.branch')) {
    def git_branch = System.getenv('GIT_BRANCH')
    println "'pact.provider.branch' not set, using environment variable GIT_BRANCH:" + git_branch
    systemProperty 'pact.provider.branch', git_branch
  }
  if (!System.getProperty('pact.verifier.publishResults')) {
    def pact_verifier_publish_results = System.getenv('PACT_VERIFIER_PUBLISH_RESULTS') ?: 'true'
    println "'pact.verifier.publishResults' not set, using environment variable PACT_VERIFIER_PUBLISH_RESULTS : " + pact_verifier_publish_results
    systemProperty 'pact.verifier.publishResults', pact_verifier_publish_results
  }
  if (!System.getProperty('pact.env')) {
    def pact_env = System.getenv('PACT_ENV')
    println "'pact.env' not set, using environment variable PACT_ENV: " + pact_env
    systemProperty 'pact.env', pact_env
  }
}

tasks.named('jacocoTestReport') {
  dependsOn tasks.named('test')
  reports {
    xml.required.set(true)
    csv.required.set(false)
    html.required.set(true)
  }
}

tasks.named('check') {
  dependsOn tasks.named('integration')
}

// check dependencies upon release ONLY
tasks.named("dependencyUpdates").configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = azureADOArtifactRepository
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(tasks.named('bootJar'))
      artifact(tasks.named('jar'))
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/$githubRepo")
      credentials {
        username = githubActor
        password = githubToken
      }
    }
    maven {
      name = "AzureArtifacts"
      url = uri(azureADOArtifactRepository)
      credentials {
        username = azureADOArtifactActor
        password = azureADOArtifactToken
      }
    }
  }
}

//Creation of Software Bill of Materials
//https://github.com/CycloneDX/cyclonedx-gradle-plugin
cyclonedxBom {
  includeConfigs = ["runtimeClasspath"]
  skipConfigs = ["compileClasspath", "testImplementation"]
  schemaVersion = "1.6"
  componentVersion = providers.provider { project.version.toString() }
  destination = file("$buildDir/reports")
}

jar {
  enabled = true
  archiveClassifier.set('plain')
  if (file("CHANGELOG.md").exists()) {
    from('CHANGELOG.md') {
      into 'META-INF'
    }
  } else {
    println "‚ö†Ô∏è  CHANGELOG.md not found, skipping inclusion in JAR"
  }
}

bootJar {
  archiveFileName = "${rootProject.name}-${project.version}.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

application {
  mainClass = 'uk.gov.hmcts.cp.Application'
}

ext {
  apiJudgesVersion="0.3.9"
  log4JVersion = "2.24.3"
  logbackVersion = "1.5.18"
  lombokVersion = "1.18.38"
}

dependencies {
  implementation "uk.gov.hmcts.cp:api-cp-refdata-courthearing-judges:$apiJudgesVersion"
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
  implementation 'io.swagger.core.v3:swagger-core:2.2.34'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-json'

  implementation 'org.springframework.cloud:spring-cloud-starter-sleuth:3.1.11'
  implementation 'com.azure.spring:spring-cloud-azure-trace-sleuth:4.20.0'

  implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
  implementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion

  implementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.5'
  implementation 'org.hibernate.validator:hibernate-validator:9.0.1.Final'
  implementation 'org.apache.commons:commons-text:1.13.1'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testImplementation(platform('org.junit:junit-bom:5.13.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.5.3', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'au.com.dius.pact.provider:junit5:4.6.17'
  testImplementation 'au.com.dius.pact.provider:spring6:4.6.17'
}
